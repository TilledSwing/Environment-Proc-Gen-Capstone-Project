Shader "CustomRenderTexture/StarCustomRenderTexture"
{
    Properties
    {
        _AngleOffset ("AngleOffset", Float) = 0
        _CellDensity ("CellDensity", Float) = 0
        _StarColor ("StarColor", Color) = (0,0,0,1)
    }
    SubShader
    {
        Pass
        {
            Tags { "RenderPipeline"="UniversalPipeline" }
            Blend One Zero
            CGPROGRAM
            
            #pragma vertex CustomRenderTextureVertexShader
            #pragma fragment frag
            #pragma target 3.0

            #include "UnityCustomRenderTexture.cginc"

            float _AngleOffset;
            float _CellDensity;
            float4 _StarColor;

            inline float2 unity_voronoi_noise_randomVector (float2 UV, float offset)
            {
                float2x2 m = float2x2(15.27, 47.63, 99.41, 89.98);
                UV = frac(sin(mul(UV, m)) * 46839.32);
                return float2(sin(UV.y*+offset)*0.5+0.5, cos(UV.x*offset)*0.5+0.5);
            }

            void Unity_Voronoi_float(float2 UV, float AngleOffset, float CellDensity, out float Out, out float Cells)
            {
                float2 g = floor(UV * CellDensity);
                float2 f = frac(UV * CellDensity);
                float t = 8.0;
                float3 res = float3(8.0, 0.0, 0.0);

                for(int y=-1; y<=1; y++)
                {
                    for(int x=-1; x<=1; x++)
                    {
                        float2 lattice = float2(x,y);
                        float2 offset = unity_voronoi_noise_randomVector(lattice + g, AngleOffset);
                        float d = distance(lattice + offset, f);
                        if(d < res.x)
                        {
                            res = float3(d, offset.x, offset.y);
                            Out = res.x;
                            Cells = res.y;
                        }
                    }
                }
            }

            float4 frag(v2f_customrendertexture i) : SV_Target
            {
                float Out, Cells;
                float2 uv = i.localTexcoord;

                Unity_Voronoi_float(uv, _AngleOffset, _CellDensity, Out, Cells);
                float voronoi = 1 - saturate(Out);
                float4 stars = pow(voronoi, 150) * _StarColor;

                return stars;
            }
            ENDCG
        }
    }
}
